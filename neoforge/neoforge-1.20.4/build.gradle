plugins {
    id('net.neoforged.gradle.userdev') version('7.0.74')
}

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)
sourceCompatibility = java_version
targetCompatibility = java_version

subsystems {
    parchment {
        minecraftVersion = '1.20.3'// TODO: Temporary until there's some way around this
        mappingsVersion = mapping_version
    }
}

dependencies {
    compileOnly("net.neoforged:neoforge:${neo_version}")
    annotationProcessor('org.spongepowered:mixin:0.8.5:processor')
    compileOnly(project(':common'))
    compileOnly(project(':vanilla:vanilla-1.20'))
    implementation(project(path: ":vanilla:vanilla-${vanilla_version}", configuration: 'shadow'))
}

tasks.withType(ProcessResources).configureEach {
    filesMatching(['META-INF/mods.toml', 'pack.mcmeta', "${project_id}.mixins.vanilla.neoforge.json".toString()]) {
        expand project.properties
    }
}

shadowJar {
    dependencies {
        include(project(":vanilla:vanilla-${vanilla_version}"))
    }
    relocate("${group_id}.${project_id}.vanilla", "${group_id}.${project_id}.vanilla.neoforge")
    destinationDirectory = file('../../target')
    archiveFileName = "${project_id}-neoforge-${minecraft_version}-${version}.jar"
}
tasks.build.dependsOn(tasks.shadowJar)
