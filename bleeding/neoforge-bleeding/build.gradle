plugins {
    id('net.neoforged.gradle.userdev') version('7.0.80')
}

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)
sourceCompatibility = targetCompatibility = java_version

subsystems {
    parchment {
        minecraftVersion = "1.20.3" // TODO: Update this
        mappingsVersion = mapping_version
    }
}

minecraft.accessTransformers.file file('src/main/resources/META-INF/neoforge.accesstransformer.cfg')

// Server Starting, command registration
minecraft.accessTransformers.entry 'public net.minecraft.server.MinecraftServer resources # resources'
minecraft.accessTransformers.entry 'public net.minecraft.server.MinecraftServer$ReloadableResources managers()net.minecraft.server.ReloadableServerResources # managers'

// Advancements
minecraft.accessTransformers.entry 'public net.minecraft.server.PlayerAdvancements player # player'

// Custom Payloads (1.20.2)
minecraft.accessTransformers.entry 'public net.minecraft.server.network.ServerCommonPacketListenerImpl server # server'

dependencies {
    compileOnly("net.neoforged:neoforge:${neo_version}")
    compileOnly(project(':common'))
    compileOnly(project(':bleeding:vanilla-bleeding'))
}

tasks.withType(ProcessResources).configureEach {
    filesMatching(['META-INF/mods.toml', 'pack.mcmeta']) {
        expand project.properties
    }
}

jar {
    destinationDirectory = file('../../target')
    archiveFileName = "${project_id}-neoforge-bleeding-${version}.jar"
}
