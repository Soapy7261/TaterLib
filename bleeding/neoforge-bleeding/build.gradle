plugins {
    id('net.neoforged.gradle.userdev') version('7.0.74')
}

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)
sourceCompatibility = targetCompatibility = java_version

subsystems {
    parchment {
        minecraftVersion = "1.20.3" // TODO: Update this
        mappingsVersion = mapping_version
    }
}

dependencies {
    compileOnly("net.neoforged:neoforge:${neo_version}")
    compileOnly(project(':common'))
    implementation(project(':vanilla:vanilla-1.20'))
    implementation(project(":vanilla:vanilla-${vanilla_version}"))
}

tasks.withType(ProcessResources).configureEach {
    filesMatching(['META-INF/mods.toml', 'pack.mcmeta', "${project_id}.mixins.neoforge.json".toString()]) {
        expand project.properties
    }
}

shadowJar {
    dependencies {
        include(project(':vanilla:vanilla-1.20'))
        include(project(":vanilla:vanilla-${vanilla_version}"))
    }
    relocate("${group_id}.${project_id}.vanilla", "${group_id}.${project_id}.vanilla.neoforge")
    destinationDirectory = file('../../target')
    archiveFileName = "${project_id}-neoforge-${minecraft_version}-${version}.jar"
}
tasks.build.dependsOn(tasks.shadowJar)
