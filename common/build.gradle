plugins {
    id('com.github.johnrengelman.shadow') version('8.1.1')
}

repositories {
    // Arclight
    maven {
        name = 'Arclight Maven'
        url = 'https://maven.izzel.io/releases'
    }

    // Mohist
    maven {
        name = 'Mohist Maven'
        url = 'https://maven.mohistmc.com/'
    }

    // NeuralNexus -- For Magma
    maven {
        name = "NeuralNexus"
        url = "https://maven.neuralnexus.dev/releases"
    }

    // Ketting
    maven {
        name = "Ketting"
        url = "https://nexus.c0d3m4513r.com/repository/Ketting/"
    }
}

dependencies {
    // YAML
    implementation('dev.dejvokep:boosted-yaml:1.3.1')

    // MySQL
    implementation("com.zaxxer:HikariCP:4.0.3")
    implementation('mysql:mysql-connector-java:8.0.28')

    // MongoDB
    implementation('org.mongodb:mongodb-driver-sync:4.9.1')
    implementation('org.mongodb:mongodb-driver-core:4.9.1')
    implementation('org.mongodb:bson:4.9.1')

    // Gson
    implementation('com.google.code.gson:gson:2.10.1')

    // Logging
    compileOnly("org.slf4j:slf4j-api:1.7.32")
    compileOnly("org.apache.logging.log4j:log4j-api:2.17.1")

    // Brigadier
    compileOnly('com.mojang:brigadier:1.0.18')

    // Mixin
    compileOnly("org.spongepowered:mixin:0.8.5")

    // LuckPerms
    compileOnly("net.luckperms:api:5.4")

    // Arclight
    compileOnly("io.izzel.arclight:arclight-api:1.5.5")

    // Ketting
    compileOnly("org.kettingpowered:kettingcore:1.2")

    // Magma
    compileOnly("org.magmafoundation:Magma-1.12.2:e586889b")

    // Mohist
    compileOnly("com.mohistmc:mohistdev:1.16.5")
}

java.disableAutoTargetJvm()

shadowJar {
    dependencies {
        include(dependency("${group_id}:${project_id}:${version}"))
        exclude('META-INF', 'META-INF/**')
        exclude('LICENSE')

        // YAML
        include(dependency('dev.dejvokep:boosted-yaml:1.3.1'))

        // MySQL
        include(dependency('com.zaxxer:HikariCP:4.0.3'))
        include(dependency('mysql:mysql-connector-java:8.0.26'))

        // MongoDB
        include(dependency('org.mongodb:mongodb-driver-sync:4.9.1'))
        include(dependency('org.mongodb:mongodb-driver-core:4.9.1'))
        include(dependency('org.mongodb:bson:4.9.1'))

        // Gson
        include(dependency('com.google.code.gson:gson:2.10.1'))
    }
    // YAML
    relocate('dev.dejvokep', "${group_id}.${project_id}.lib.dejvokep")
    relocate('org.intellij', "${group_id}.${project_id}.lib.intellij")
    relocate('org.jetbrains', "${group_id}.${project_id}.lib.jetbrains")

    // MySQL
    relocate('com.zaxxer.hikari', "${group_id}.${project_id}.lib.hikari")
    relocate('com.mysql', "${group_id}.${project_id}.lib.mysql")

    // MongoDB
    relocate('com.mongodb', "${group_id}.${project_id}.lib.mongodb")
    relocate('org.bson', "${group_id}.${project_id}.lib.bson")

    // Gson
    relocate('com.google.gson', "${group_id}.${project_id}.lib.gson")

    destinationDirectory = file('../target')
    archiveFileName = "${project_id}-common-${version}.jar"
}

artifacts {
    archives(shadowJar)
}
