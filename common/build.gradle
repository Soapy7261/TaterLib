plugins {
    id('com.github.johnrengelman.shadow') version('8.1.1')
}

dependencies {
    // YAML
    implementation('dev.dejvokep:boosted-yaml:1.3.1')

    // MySQL
    implementation("com.zaxxer:HikariCP:4.0.3")
    implementation("mysql:mysql-connector-java:8.0.26")

    // MongoDB
    implementation('org.mongodb:mongodb-driver-sync:4.9.1')
    implementation('org.mongodb:mongodb-driver-core:4.9.1')
    implementation('org.mongodb:bson:4.9.1')

    // Gson
    implementation('com.google.code.gson:gson:2.10.1')

    // LuckPerms
    compileOnly("net.luckperms:api:5.4")
}

shadowJar {
    dependencies {
        // YAML
        include(dependency("dev.dejvokep:boosted-yaml:1.3.1"))

        // MySQL
        include(dependency("com.zaxxer:HikariCP:4.0.3"))
        include(dependency("mysql:mysql-connector-java:8.0.26"))

        // MongoDB
        include(dependency('org.mongodb:mongodb-driver-sync:4.9.1'))
        include(dependency('org.mongodb:mongodb-driver-core:4.9.1'))
        include(dependency('org.mongodb:bson:4.9.1'))

        // Gson
        include(dependency('com.google.code.gson:gson:2.10.1'))
    }
    // YAML
    relocate('dev.dejvokep', 'dev.neuralnexus.taterlib.lib.dejvokep')

    // MySQL
    relocate('com.zaxxer.hikari', 'dev.neuralnexus.taterlib.lib.hikari')
    relocate('com.mysql', 'dev.neuralnexus.taterlib.lib.mysql')

    // MongoDB
    relocate('com.mongodb', 'dev.neuralnexus.taterlib.lib.mongodb')
    relocate('org.bson', 'dev.neuralnexus.taterlib.lib.bson')

    // Gson
    relocate('com.google.gson', 'dev.neuralnexus.taterlib.lib.gson')

    destinationDirectory = file('../target')
    archiveFileName = "TaterLib-${version}-common.jar"
}
shadowJar.doLast {
    task ->
        ant.checksum(file: task.archivePath) {
            hashAlgorithm = 'md5'
            property = 'md5sum'
        }
}

artifacts {
    archives(shadowJar)
}
