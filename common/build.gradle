tasks.named('test', Test) {
    useJUnitPlatform()
    maxHeapSize = '1G'
}

repositories {
    // Arclight
    maven {
        name = 'Arclight Maven'
        url = 'https://maven.izzel.io/releases'
    }

    // Mohist
    maven {
        name = 'Mohist Maven'
        url = 'https://maven.mohistmc.com/'
    }

    // NeuralNexus -- For Magma
    maven {
        name = "NeuralNexus"
        url = "https://maven.neuralnexus.dev/releases"
    }

    // Ketting
    maven {
        name = "Ketting"
        url = "https://nexus.c0d3m4513r.com/repository/Ketting/"
    }
}

dependencies {
    // Project
    compileOnly(project(':tooling:entrypoint-spoof'))

    // Test
    testImplementation('org.junit.jupiter:junit-jupiter:5.7.1')
    testRuntimeOnly('org.junit.platform:junit-platform-launcher')

    // Config
    implementation('org.spongepowered:configurate-hocon:4.1.2')

    // bStats
    implementation('org.bstats:bstats-base:3.0.2')
    implementation('org.bstats:bstats-bukkit:3.0.2')
    implementation('org.bstats:bstats-bungeecord:3.0.2')
    implementation('org.bstats:bstats-sponge:3.0.2')
    implementation('org.bstats:bstats-velocity:3.0.2')

    // MySQL
    implementation("com.zaxxer:HikariCP:4.0.3")
    implementation('com.mysql:mysql-connector-j:8.3.0')

    // MongoDB
    implementation('org.mongodb:mongodb-driver-sync:4.9.1')
    implementation('org.mongodb:mongodb-driver-core:4.9.1')
    implementation('org.mongodb:bson:4.9.1')

    // Gson
    implementation('com.google.code.gson:gson:2.10.1')

    // Logging
    compileOnly("org.slf4j:slf4j-api:1.7.32")
    compileOnly("org.apache.logging.log4j:log4j-api:2.17.1")

    // Brigadier
    compileOnly('com.mojang:brigadier:1.0.18')

    // Mixin
    compileOnly("org.spongepowered:mixin:0.8.5")

    // LuckPerms
    compileOnly("net.luckperms:api:5.4")

    // Arclight
    compileOnly("io.izzel.arclight:arclight-api:1.5.5")

    // Ketting
    compileOnly("org.kettingpowered:kettingcore:1.2")

    // Magma
    compileOnly("org.magmafoundation:Magma-1.12.2:e586889b")

    // Mohist
    compileOnly("com.mohistmc:mohistdev:1.16.5")
}

java.disableAutoTargetJvm()

shadowJar {
    dependencies {
        include(dependency("${group_id}:${project_id}:${version}"))
        exclude('META-INF', 'META-INF/**')
        exclude('LICENSE')

        // Config
        include(dependency('org.spongepowered:configurate-core:4.1.2'))
        include(dependency('org.spongepowered:configurate-hocon:4.1.2'))
        include(dependency('com.typesafe:config:1.4.1'))

        // bStats
        include(dependency('org.bstats:bstats-base:3.0.2'))
        include(dependency('org.bstats:bstats-bukkit:3.0.2'))
        include(dependency('org.bstats:bstats-bungeecord:3.0.2'))
        include(dependency('org.bstats:bstats-sponge:3.0.2'))
        include(dependency('org.bstats:bstats-velocity:3.0.2'))

        // MySQL
        include(dependency('com.zaxxer:HikariCP:4.0.3'))
        include(dependency('com.mysql:mysql-connector-j:8.3.0'))

        // MongoDB
        include(dependency('org.mongodb:mongodb-driver-sync:4.9.1'))
        include(dependency('org.mongodb:mongodb-driver-core:4.9.1'))
        include(dependency('org.mongodb:bson:4.9.1'))

        // Gson
        include(dependency('com.google.code.gson:gson:2.10.1'))
    }
    // Config
    relocate('org.spongepowered.configurate', "${group_id}.${project_id}.lib.configurate")
    relocate('com.typesafe.config', "${group_id}.${project_id}.lib.typesafe.config")
//    relocate('org.intellij', "${group_id}.${project_id}.lib.intellij")
//    relocate('org.jetbrains', "${group_id}.${project_id}.lib.jetbrains")

    // bStats
    relocate('org.bstats', "${group_id}.${project_id}.lib.bstats")

    // MySQL
    relocate('com.zaxxer.hikari', "${group_id}.${project_id}.lib.hikari")
    relocate('com.mysql', "${group_id}.${project_id}.lib.mysql")

    // MongoDB
    relocate('com.mongodb', "${group_id}.${project_id}.lib.mongodb")
    relocate('org.bson', "${group_id}.${project_id}.lib.bson")

    // Gson
    relocate('com.google.gson', "${group_id}.${project_id}.lib.gson")

    destinationDirectory = file('../target')
    archiveFileName = "${project_id}-common-${version}.jar"
}

artifacts {
    archives(shadowJar)
}
