plugins {
    id('com.github.johnrengelman.shadow') version('8.1.1')
}

repositories {
    // Arclight
    maven {
        name = 'Arclight Maven'
        url = 'https://maven.izzel.io/releases'
    }

    // Mohist
    maven {
        name = 'Mohist Maven'
        url = 'https://maven.mohistmc.com/'
    }

    // NeuralNexus Private
    maven {
        name = "NeuralNexus"
        url = "https://maven.neuralnexus.dev/releases"
    }
}

dependencies {
    // YAML
    implementation('dev.dejvokep:boosted-yaml:1.3.1')

    // MySQL
    implementation("com.zaxxer:HikariCP:4.0.3")
    implementation("mysql:mysql-connector-java:8.0.26")

    // MongoDB
    implementation('org.mongodb:mongodb-driver-sync:4.9.1')
    implementation('org.mongodb:mongodb-driver-core:4.9.1')
    implementation('org.mongodb:bson:4.9.1')

    // Gson
    implementation('com.google.code.gson:gson:2.10.1')

    // Logging
    implementation("org.slf4j:slf4j-api:1.7.32")
    implementation("org.slf4j:slf4j-simple:1.7.32")

    // Brigadier
    compileOnly('com.mojang:brigadier:1.0.18')

    // LuckPerms
    compileOnly("net.luckperms:api:5.4")

    // Arclight API
    compileOnly("io.izzel.arclight:arclight-api:1.5.5")

    // Magma API
    compileOnly("org.magmafoundation:Magma-1.12.2:e586889b")

    // Mohist API
    compileOnly("com.mohistmc:mohistdev:1.16.5")
}

shadowJar {
    dependencies {
        // YAML
        include(dependency("dev.dejvokep:boosted-yaml:1.3.1"))

        // MySQL
        include(dependency("com.zaxxer:HikariCP:4.0.3"))
        include(dependency("mysql:mysql-connector-java:8.0.26"))

        // MongoDB
        include(dependency('org.mongodb:mongodb-driver-sync:4.9.1'))
        include(dependency('org.mongodb:mongodb-driver-core:4.9.1'))
        include(dependency('org.mongodb:bson:4.9.1'))

        // Gson
        include(dependency('com.google.code.gson:gson:2.10.1'))

        // Logging
        include(dependency("org.slf4j:slf4j-api:1.7.32"))
        include(dependency("org.slf4j:slf4j-simple:1.7.32"))
    }
    // YAML
    relocate('dev.dejvokep', "${group_id}.${project_id}.lib.dejvokep")

    // MySQL
    relocate('com.zaxxer.hikari', "${group_id}.${project_id}.lib.hikari")
    relocate('com.mysql', "${group_id}.${project_id}.lib.mysql")

    // MongoDB
    relocate('com.mongodb', "${group_id}.${project_id}.lib.mongodb")
    relocate('org.bson', "${group_id}.${project_id}.lib.bson")

    // Gson
    relocate('com.google.gson', "${group_id}.${project_id}.lib.gson")

    // Logging
    relocate('org.slf4j', "${group_id}.${project_id}.lib.slf4j")

    destinationDirectory = file('../target')
    archiveFileName = "${project_name}-common-${version}.jar"
}
shadowJar.doLast {
    task ->
        ant.checksum(file: task.archivePath) {
            hashAlgorithm = 'md5'
            property = 'md5sum'
        }
}

artifacts {
    archives(shadowJar)
}
