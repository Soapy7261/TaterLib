plugins {
    alias(libs.plugins.spotless)
}

dependencies {
    compileOnly('org.spongepowered:mixin:0.8.5')
    compileOnly('org.ow2.asm:asm-tree:6.2')
    compileOnly(project(':common'))
    compileOnly(project(':tooling:entrypoint-spoof'))
}

spotless {
    format 'misc', {
        target '*.gradle', '.gitattributes', '.gitignore'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
    }
    java {
        importOrder()
        removeUnusedImports()
        cleanthat()
        googleJavaFormat('1.17.0').aosp().formatJavadoc(true).reorderImports(true)
        formatAnnotations()
        licenseHeader """/**
 * Copyright (c) \$YEAR Dylan Sperrer - dylan@sperrer.ca
 * The project is Licensed under <a href="https://github.com/p0t4t0sandwich/TaterLib/blob/dev/LICENSE">GPL-3</a>
 * The API is Licensed under <a href="https://github.com/p0t4t0sandwich/TaterLib/blob/dev/LICENSE-API">MIT</a>
 */

"""
    }
}

tasks.build.dependsOn(spotlessApply)

tasks.withType(ProcessResources).configureEach {
    filesMatching([
            'plugin.yml',
            'bungee.yml',
            'fabric.mod.json', "${project.project_id}.mixins.json".toString(),
            'META-INF/mods.toml', 'mcmod.info', 'pack.mcmeta',
            'META-INF/sponge_plugins.json',
            'velocity-plugin.json'
    ]) {
        expand project.properties
    }
}

jar {
//    exclude('fabric.mod.json')
//    exclude('META-INF/mods.toml')
//    exclude('mcmod.info')
//    exclude('pack.mcmeta')
//    exclude('META-INF/sponge_plugins.json')
    exclude('velocity-plugin.json')

    archiveFileName = "${project_id}-loader-${loader_version}.jar"
}
