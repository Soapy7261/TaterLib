import net.fabricmc.loom.task.RemapJarTask

plugins {
    id('fabric-loom') version('1.4-SNAPSHOT')
}

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)
sourceCompatibility = targetCompatibility = java_version

//base {
//    libsDirectory = file('../../target')
//    archivesName = "${project_id}-fabric-${minecraft_version}"
//}

dependencies {
    compileOnly(project(':common'))
    implementation(project(":vanilla:vanilla-${vanilla_version}"))

    // Minecraft and Fabric mappings
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings loom.layered() {
        officialMojangMappings()
        parchment("org.parchmentmc.data:parchment-${mapping_version}@zip")
    }
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

    // Fabric API
    Set<String> apiModules = ['fabric-command-api-v2']
    apiModules.forEach {
        modImplementation(fabricApi.module(it, project.fabric_version))
    }

    // Lucko's Permissions API
    include(modImplementation('me.lucko:fabric-permissions-api:0.2-SNAPSHOT'))
}

tasks.withType(ProcessResources).configureEach {
    filesMatching(['fabric.mod.json', "${project_id}.mixins.fabric_1_20.json".toString()]) {
        expand project.properties
    }
}

loom {
    mixin {
        defaultRefmapName.set("${project_id}.mixins.fabric.refmap_1_20.json")
    }
}

shadowJar {
    dependencies {
        include(project(':vanilla:vanilla-1.20'))
        exclude('/mappings/*')
    }
    relocate("${group_id}.${project_id}.vanilla", "${group_id}.${project_id}.vanilla.fabric")
}

task remappedShadowJar(type: RemapJarTask) {
    dependsOn(tasks.shadowJar)
    input = tasks.shadowJar.archiveFile
    destinationDirectory = file('../../target')
    archiveFileName = "${project_id}-fabric-${minecraft_version}-${version}.jar"
}
tasks.build.dependsOn(tasks.remappedShadowJar)
