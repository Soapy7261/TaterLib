plugins {
    id('fabric-loom') version('1.4-SNAPSHOT')
}

base {
    archivesName = "${project_id}-fabric-${minecraft_version}"
}

dependencies {
    compileOnly(project(':common'))

    // Minecraft and Fabric mappings
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings("net.fabricmc:yarn:${project.yarn_mappings}")
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

    // Fabric API
    Set<String> apiModules = [
            'fabric-api-base',
            'fabric-command-api-v1',
            'fabric-lifecycle-events-v1',
            'fabric-networking-api-v1'
    ]

    apiModules.forEach {
        modImplementation(fabricApi.module(it, project.fabric_version))
    }

    // Lucko's Permissions API
    include(modImplementation('me.lucko:fabric-permissions-api:0.2-SNAPSHOT'))
}

tasks.withType(ProcessResources).configureEach {
    filesMatching(['fabric.mod.json', "${project_id}.mixins.fabric.v1_14.json".toString()]) {
        expand project.properties
    }
}

loom {
    mixin {
        defaultRefmapName.set("${project_id}.mixins.fabric.v1_14.refmap.json")
    }
}