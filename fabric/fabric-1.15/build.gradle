plugins {
    alias(libs.plugins.unimined)
}

base {
    archivesName = "${project_id}-fabric-${minecraft_version}"
}

unimined.minecraft(sourceSets.main) {
    version(minecraft_version)
    fabric {
        loader(loader_version)
    }
    mappings {
        yarn(2)
    }
    defaultRemapJar = false
    remap(jar) {
        prodNamespace("intermediary")
        mixinRemap {
            disableRefmap()
        }
    }
}

dependencies {
    Set<String> apiModules = [
            'fabric-api-base',
            'fabric-command-api-v1',
            'fabric-lifecycle-events-v1',
            'fabric-networking-api-v1'
    ]

    apiModules.forEach {
        modImplementation(fabricApi.fabricModule(it, project.fabric_version))
    }

    // Lucko's Permissions API
    include(modImplementation('me.lucko:fabric-permissions-api:0.2-SNAPSHOT'))

    compileOnly(project(':api'))
    compileOnly(project(':common'))
    compileOnly(project(':tooling:conditional-mixins'))
    compileOnly(project(':tooling:loader'))
}
