plugins {
    alias(libs.plugins.shadow)
    alias(libs.plugins.unimined)
}

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)
sourceCompatibility = targetCompatibility = java_version

unimined.minecraft(sourceSets.main) {
    version(minecraft_version)
    fabric {
        loader(loader_version)
    }
    mappings {
        mojmap()
    }
    defaultRemapJar = false
    remap(shadowJar) {
        prodNamespace("intermediary")
        mixinRemap {
            disableRefmap()
        }
    }
}

dependencies {
    Set<String> apiModules = [
            'fabric-command-api-v2',
            'fabric-lifecycle-events-v1',
            'fabric-networking-api-v1'
    ]

    apiModules.forEach {
        modImplementation(fabricApi.fabricModule(it, project.fabric_version))
    }

    // Lucko's Permissions API
    include(modImplementation('me.lucko:fabric-permissions-api:0.2-SNAPSHOT'))

    compileOnly(project(':api'))
    compileOnly(project(':common'))
    compileOnly(project(':tooling:conditional-mixins'))
    compileOnly(project(':tooling:loader'))
    implementation(project(':vanilla:vanilla-1.21'))
}

shadowJar {
    dependencies {
        exclude('/mappings/*')
        include(project(':vanilla:vanilla-1.21'))
    }
    relocate("dev.neuralnexus.taterlib.v1_21.vanilla", "dev.neuralnexus.taterlib.v1_21.vanilla.fabric")
}
