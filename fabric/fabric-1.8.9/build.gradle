plugins {
    id('fabric-loom') version('1.4-SNAPSHOT')
    id("legacy-looming") version("1.4-SNAPSHOT")
}

dependencies {
    compileOnly(project(':common'))

    // Minecraft and Fabric mappings
    minecraft("com.mojang:minecraft:${project.minecraft_version}")
    mappings("net.legacyfabric:yarn:${project.yarn_mappings}:v2")
    modImplementation("net.fabricmc:fabric-loader:${project.loader_version}")

    // Legacy Fabric API
    Set<String> apiModules = [
            'legacy-fabric-api-base',
            'legacy-fabric-command-api-v2',
            'legacy-fabric-lifecycle-events-v1',
            'legacy-fabric-networking-api-v1',
            'legacy-fabric-permissions-api-v1'
    ]

    apiModules.forEach {
        modImplementation(legacy.apiModule(it, project.fabric_version))
    }
}

tasks.withType(ProcessResources).configureEach {
    filesMatching(['fabric.mod.json', "${project_id}.mixins.json".toString()]) {
        expand project.properties
    }
}

jar {
    destinationDirectory = file('../../target')
    archiveFileName = "${project_id}-fabric-${minecraft_version}-${version}.jar"
}
