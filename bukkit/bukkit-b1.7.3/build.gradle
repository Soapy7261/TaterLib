plugins {
    id('com.github.johnrengelman.shadow') version('8.1.1')
}

repositories {
    // NeuralNexus Private
    maven {
        name = "NeuralNexusPrivate"
        url = "https://maven.neuralnexus.dev/private"
        credentials {
            username "$NeuralNexusPrivateUsername"
            password "$NeuralNexusPrivatePassword"
        }
    }
}

dependencies {
    implementation(project(path: ':common', configuration: 'shadow'))

    // CraftBukkit
    compileOnly("org.bukkit:craftbukkit:${minecraft_version}-${craftbukkit_version}")
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            project_id         : project_id,
            project_name       : project_name,
            project_description: project_description,
            group_id           : group_id,
            version            : version,
            authors            : authors,
    ]
    inputs.properties replaceProperties

    filesMatching(['plugin.yml']) {
        expand replaceProperties + [project: project]
    }
}

shadowJar {
    dependencies {
        include(project(":common"))
    }

    destinationDirectory = file('../../target')
    archiveFileName = "${project_name}-bukkit-${minecraft_version}-${version}.jar"
}
shadowJar.doLast {
    task ->
        ant.checksum(file: task.archivePath) {
            hashAlgorithm = 'md5'
            property = 'md5sum'
        }
}

artifacts {
    archives(shadowJar)
}
