import org.spongepowered.gradle.plugin.config.PluginLoaders
import org.spongepowered.plugin.metadata.model.PluginDependency

plugins {
    id('com.github.johnrengelman.shadow') version('8.1.1')
    id('java-library')
    id("org.spongepowered.gradle.plugin") version('2.1.1')
}

targetCompatibility = java_version

sponge {
    apiVersion(spongeapi_version)
    license(project.license)
    loader {
        name(PluginLoaders.JAVA_PLAIN)
        version("1.0")
    }
    plugin(project_id) {
        version("${minecraft_version}-${project.version}")
        displayName(project_name)
        entrypoint("${group_id}.${project_id}.sponge.Sponge${project_name}Plugin")
        description(project_description)
        links {
            homepage(homepage_url)
            source(repo_url)
            issues(issue_tracker_url)
        }
        dependency("spongeapi") {
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(false)
        }
        dependency("luckperms") {
            version("5.3.0")
            loadOrder(PluginDependency.LoadOrder.AFTER)
            optional(true)
        }
    }
}

dependencies {
    implementation(project(path: ':common', configuration: 'shadow'))

    // Brigadier
    compileOnly('com.mojang:brigadier:1.0.18')
}

shadowJar {
    dependencies {
        include(project(":common"))
    }

    destinationDirectory = file('../../target')
    archiveFileName = "${project_name}-sponge10-${version}.jar"
}

artifacts {
    archives(shadowJar)
}
