plugins {
    alias(libs.plugins.shadow)
    alias(libs.plugins.unimined)
}

base {
    archivesName = "${project_id}-forge-${minecraft_version}"
}

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)
sourceCompatibility = targetCompatibility = java_version

unimined.minecraft(sourceSets.main) {
    version(minecraft_version)
    minecraftForge {
        loader(forge_version)
        mixinConfig("taterlib.mixins.v1_19_3.forge.patch.json")
    }
    mappings {
        mojmap()
    }
    defaultRemapJar = true
    remap(shadowJar) {
        prodNamespace("searge")
        mixinRemap {
            disableRefmap()
        }
        archiveFileName = "${project_id}-forge-${minecraft_version}-${project.version}.jar"
    }
}

tasks.build.dependsOn(remapShadowJar)

dependencies {
    compileOnly(project(':api'))
    compileOnly(project(':common'))
    compileOnly(project(':tooling:conditional-mixins'))
    compileOnly(project(':tooling:loader'))
    compileOnly(project(':forge:forge-utils-modern'))
    compileOnly(project(':vanilla:vanilla-1.19'))
}

shadowJar {
    dependencies {
        include(project(":vanilla:vanilla-${minecraft_version}"))
    }
    relocate("dev.neuralnexus.taterlib.v1_19.vanilla", "dev.neuralnexus.taterlib.v1_19.vanilla.forge")
    archiveFileName = "${project_id}-forge-${minecraft_version}-${version}-dev.jar"
}
