plugins {
    id('net.minecraftforge.gradle') version('[6.0,6.2)')
    id('org.spongepowered.mixin') version('0.7-SNAPSHOT')
}

java.toolchain.languageVersion = JavaLanguageVersion.of(java_version)
sourceCompatibility = targetCompatibility = java_version

minecraft {
    mappings(channel: mapping_channel, version: mapping_version)
}

dependencies {
    minecraft("net.minecraftforge:forge:${minecraft_version}-${forge_version}")
    annotationProcessor('org.spongepowered:mixin:0.8.5:processor')
    compileOnly(project(':common'))
    compileOnly(project(':forge:forge-utils-modern'))
    compileOnly(project(':forge:forge-1.20'))
    compileOnly(project(':vanilla:vanilla-1.20'))
    implementation(project(':vanilla:vanilla-1.20.2'))
}

mixin {
    add(sourceSets.main, "taterlib.mixins.v1_20_2.forge.refmap.json")
    config("taterlib.mixins.v1_20_2.forge.json")
}

shadowJar {
    dependencies {
        include(project(':vanilla:vanilla-1.20.2'))
    }
    relocate("dev.neuralnexus.taterlib.v1_20.vanilla", "dev.neuralnexus.taterlib.v1_20.vanilla.forge")
    relocate("dev.neuralnexus.taterlib.v1_20_2.vanilla", "dev.neuralnexus.taterlib.v1_20_2.vanilla.forge")
    archiveFileName = "${project_id}-forge-${minecraft_version}-${version}.jar"
}

reobf {
    shadowJar {}
}

tasks.shadowJar.finalizedBy('reobfShadowJar')
tasks.assemble.dependsOn('reobfShadowJar')
